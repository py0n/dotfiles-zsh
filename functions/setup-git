# Gitのviewer設定。
# http://motemen.hatenablog.com/entries/2013/11/26
# http://qiita.com/yamagen0915/items/eb8275275fc87935843b

if [[ -x $(whence git) ]]; then

    # よしなにcheckout
    # bashで書くこと
    function gitcheckout() {
        if [[ $# == 0 && -x $(which peco) ]]; then
            local branch=$(git branch --all | grep '^[^\*]' | peco | awk '{print $NF}')
            if [[ "${branch}" =~ 'remotes/' ]]; then
                local localbranch=$(echo ${branch} | sed -e 's/^remotes\/[^\/]*\///')
                local remotebranch=$(echo ${branch} | sed -e 's/^remotes\///')
                git checkout -b ${localbranch} ${remotebranch}
            else
                git checkout ${branch}
            fi
        else
            git checkout ${@}
        fi
    }

    local gitconfig='git config --global --replace-all'

    # エイリアス設定。
    ${gitconfig} alias.bd "!f(){ if [[ \$# == 0 && -x \$(which peco) ]]; then git branch -d \$(git branch | awk '/^[^*]/{print \$1}' | peco); else git branch -d \${@}; fi; };f"
    ${gitconfig} alias.br branch
    ${gitconfig} alias.bv "branch -vv"
    ${gitconfig} alias.cb "checkout -b"
    ${gitconfig} alias.ci commit
    ${gitconfig} alias.co "!$(where gitcheckout);gitcheckout"
    ${gitconfig} alias.di "diff --find-renames --ignore-space-change"
    ${gitconfig} alias.ds "diff --find-renames --ignore-space-change --staged"
    ${gitconfig} alias.lg "log --decorate --graph --stat"
    ${gitconfig} alias.st "status --branch --short"
    ${gitconfig} color.ui true
    ${gitconfig} core.excludesfile "~/.gitignore"

    # Global Alias
    # http://qiita.com/sona-tar/items/fe401c597e8e51d4e243
    if [[ -x $(whence peco) ]]; then
        alias -g @b='$(git branch --all | grep "^[^\*]" | peco | awk "{print \$NF}")'
        alias -g @h='$(git log --oneline --branches | peco | awk "{print $1}")'
        alias -g @m='$(git status --porcelain | peco | awk "{print \$NF}")'
    fi

    if [[ -x $(whence diff-highlight) && -x $(whence less) ]]; then
        # viewer設定。
        local convertor=''
        if [[ -x $(whence nkf) ]]; then
            convertor='| nkf -w '
        elif [[ -x $(whence lv) ]]; then
            convertor='| lv -Ou '
        fi
        local pager="diff-highlight ${convertor}| LESS=-R less"
        ${gitconfig} pager.diff "${pager}"
        ${gitconfig} pager.log  "${pager}"
        ${gitconfig} pager.show "${pager}"
    fi
fi

# vim: expandtab filetype=zsh shiftwidth=4 softtabstop=4 ts=4
